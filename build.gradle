apply plugin: "java"

version '1.0'

targetCompatibility = 8
sourceCompatibility = JavaVersion.VERSION_18

sourceSets.main.java.srcDirs = ["src"]

repositories {
    mavenCentral()
    maven{ url 'https://www.jitpack.io' }
}

ext {
    //the build number that this mod is made for
    mindustryVersion = 'v142'
    jabelVersion = "93fde537c7"
}

//java 8 backwards compatibility flag
allprojects {
    tasks.withType(JavaCompile).tap {
        configureEach {
            options.compilerArgs.addAll(['--release', '8'])
        }
    }
}

dependencies {
    compileOnly "com.github.Anuken.Arc:arc-core:$mindustryVersion"
    compileOnly "com.github.Anuken.MindustryJitpack:core:$mindustryVersion"

    annotationProcessor "com.github.Anuken:jabel:$jabelVersion"
}

//force arc version
configurations.configureEach {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == 'com.github.Anuken.Arc') {
            details.useVersion "$mindustryVersion"
        }
    }
}

jar {
    archiveFileName = "UnlockTechTreeMod-v" + project.getVersion() + ".jar";

    from {
        configurations.runtimeClasspath.collect{ it.isDirectory() ? it : zipTree(it) }
    }

    from(rootDir) {
        include "mod.hjson"
    }

    from("assets/") {
        include "**"
    }
}

tasks.register('deploy', Jar) {
    dependsOn jar
    archiveFileName = "${project.archivesBaseName}.jar"

    from { zipTree("$buildDir/libs/UnlockTechTreeMod-v" + project.getVersion() + ".jar") }

    doLast {
        delete {
            delete "$buildDir/libs/UnlockTechTreeMod-v" + project.getVersion() + ".jar"
        }
    }
}
